import "blueprint.node" for Node
import "blueprint.pin" for Pin
import "blueprint.node_param" for NodeParam
import "blueprint.variant" for Variant, VAR_TYPE_NUMBER
import "blueprint.blueprint" for Blueprint
import "blueprint.node_editor" for NodeEditor
import "blueprint.nodes.number2" for Number2
import "blueprint.node_helper" for NodeHelper
import "sketchgraph.variant" for VAR_TYPE_GEOMETRY, VarGeometry
import "sketchgraph.context" for CONTEXT
import "sketchgraph.util" for DRAW_SCALE
import "sketchgraph.geo_helper" for GeoHelper
import "geometry" for Point as tt_Point

var EDITOR = nil

class Point is Node
{
	init()
	{
		super.init()

		this.imports = [
			Pin(this, "x", VAR_TYPE_NUMBER),
			Pin(this, "y", VAR_TYPE_NUMBER),
		]
		this.exports = [
			Pin(this, "geo", VAR_TYPE_GEOMETRY),
		]

		this.params.add(NodeParam("x", 0))
		this.params.add(NodeParam("y", 0))

		this.layout()

		this.geo_id = CONTEXT.advance_geo_id()

		this.editor_pos = Number2()
	}

	calc_value(idx)
	{
		var x = NodeHelper.calc_prop_value(this, "x")
		var y = NodeHelper.calc_prop_value(this, "y")

		var shape = tt_Point.init(x, y)

		var geo = VarGeometry(this.geo_id, shape)
		GeoHelper.set_color(geo, this.selected)
		return Variant(VAR_TYPE_GEOMETRY, geo)
	}

	on_selected() 
	{
		super.on_selected()

		Blueprint.send_pin_dirty_root(this.exports[0])
	}

	on_deselected() 
	{
		super.on_deselected()

		Blueprint.send_pin_dirty_root(this.exports[0])
	}

	bind_editor()
	{
		if (!EDITOR) {
			EDITOR = NodeEditor("assets/blueprints/editorgraph/select_and_translate.ves")
		}

		var x = NodeHelper.calc_prop_value(this, "x") * DRAW_SCALE
		var y = NodeHelper.calc_prop_value(this, "y") * DRAW_SCALE
		this.editor_pos.value.set(x, y)

		Blueprint.connect(this.editor_pos.exports[0], EDITOR.editor_node.imports[1])

		Blueprint.send_node_dirty_root(EDITOR.editor_node)

		return true
	}

	draw_editor() 
	{
		if (EDITOR) {
			EDITOR.draw()		
		}
	}

	on_mouse_pressed(x, y, btn) 
	{
		EDITOR.on_mouse_pressed(x, y, btn)
	}

	on_mouse_released(x, y, btn) 
	{
		EDITOR.on_mouse_released(x, y, btn)
	}

	on_mouse_dragged(x, y, btn) 
	{
		EDITOR.on_mouse_dragged(x, y, btn)

		this.query_param("x").value = this.editor_pos.value.x / DRAW_SCALE
		this.query_param("y").value = this.editor_pos.value.y / DRAW_SCALE

		Blueprint.send_pin_dirty_root(this.exports[0])
	}
}