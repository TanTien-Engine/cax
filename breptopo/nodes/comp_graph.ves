import "blueprint.node" for Node
import "blueprint.pin" for Pin
import "blueprint.blueprint" for Blueprint
import "blueprint.variant" for VAR_TYPE_ANY, Variant
import "blueprint.variant_proxy" for VariantProxy
import "blueprint.dirty_types" for DIRTY_CONNECTION, DIRTY_GUI
import "graph.nodes.draw" for Draw
import "partgraph.node_helper" for NodeHelper
import "logger.logger" for Logger
import "partgraph.variant" for VAR_TOPO_SHAPE
import "breptopo.variant" for VAR_COMP_GRAPH, VAR_HIST_GRAPH
import "breptopo.compiler" for Compiler
import "logger.logger" for Logger
import "breptopo" for CompGraph as tt_CompGraph
import "graph" for GraphTools
import "partgraph" for TopoShape

class CompGraph is Node
{
	init()
	{
		super.init()

		this.imports = [
			Pin(this, "val", VAR_TYPE_ANY),
			Pin(this, "hist", VAR_HIST_GRAPH),
		]
		this.exports = [
			Pin(this, "val", VAR_TYPE_ANY),
		]

		this.layout()

		this.graph = nil
		this.node_idx = -1

		this.compiler = nil

		this.n_draw = Draw()
	}

	on_pin_dirty(pin, type, msg)
	{
		super.on_pin_dirty(pin, type, msg)

		if (type == DIRTY_CONNECTION)
		{
			// msg is conn
			if (this.compiler)
			{
				this.compile_graph_partial(msg.from.node)
			}
		}
		else if (type == DIRTY_GUI)
		{
			// reuse graph
		}
		else
		{
			this.graph = nil
			this.node_idx = -1
		}
	}

	calc_value(idx)
	{
		if (!this.graph) {
			this.build_graph()
		}

		var v_hist = Blueprint.calc_input_value(this, 1)

		var val = this.graph.eval(this.node_idx, v_hist.value)
		return this.return_value(val)
	}

	return_value(val)
	{
		if (val is List)
		{
			var array = []
			for (var v in val) {
				array.add(this.return_value(v))
			}
			return Variant(array)
		}
		else if (val is TopoShape)
		{
			return Variant(VAR_TOPO_SHAPE, val)
		}
		else
		{
			return Variant(val)
		}
	}

	build_graph()
	{
		Logger.info("computational graph compile")

		this.graph = tt_CompGraph.init()
		this.compile_graph_no_recursion()

		var graph = this.graph.get_graph()
		GraphTools.layout(graph, "hierarchy")

		Blueprint.connect(VariantProxy (Variant(VAR_COMP_GRAPH, graph)), "out", this.n_draw, "graph")
	}

	compile_graph_recursion()
	{
		this.compiler = Compiler(this.graph)
		this.compiler.recursion = true
		this.node_idx = NodeHelper.compile_graph(this, 0, this.compiler)
	}

	compile_graph_no_recursion()
	{
		this.compiler = Compiler(this.graph)
		this.compiler.recursion = false

		var t_node = Blueprint.get_input_node(this, 0)
		var prev_nodes = Blueprint.get_precursor_nodes(t_node)
		var sorted_nodes = Blueprint.topo_sort(prev_nodes)
		for (var node in sorted_nodes)
		{
			if (node.has_method("compile_graph(_,_)")) 
			{
				var id = node.compile_graph(this.compiler, 0)
				var key = "%(node.to_pointer())_0"
				this.compiler.evaluated[key] = id
				this.node_idx = id
			}
		}
	}

	compile_graph_partial(start_node)
	{
		var t_node = Blueprint.get_input_node(this, 0)
		var prev_nodes = Blueprint.get_precursor_nodes(t_node)
		var sorted_nodes = Blueprint.topo_sort(prev_nodes)
		var start = false

		for (var node in sorted_nodes)
		{
			if (node == start_node)
			{
				start = true

				var graph = this.graph.get_graph()
				var key = "%(node.to_pointer())_0"
				graph.clear_edges(this.compiler.evaluated[key])
			}
			else if (start and node.has_method("compile_graph(_,_)")) 
			{
				var id = node.compile_graph(this.compiler, 0)
				var key = "%(node.to_pointer())_0"
				this.compiler.evaluated[key] = id
				this.node_idx = id
			}
		}

		var graph = this.graph.get_graph()
		GraphTools.layout(graph, "hierarchy")
	}

	draw(mt, cam_mt)
	{
		super.draw(mt, cam_mt)

		this.n_draw.draw_preview()
	}

	bind_editor()
	{
		return this.n_draw.bind_editor()
	}

	draw_editor() 
	{
		this.n_draw.draw_editor()
	}

	on_mouse_pressed(x, y, btn) 
	{
		this.n_draw.on_mouse_pressed(x, y, btn)

		this.update_selected()
	}

	on_mouse_dragged(x, y, btn) 
	{
		this.n_draw.on_mouse_dragged(x, y, btn)
	}

	on_mouse_scrolled(x, y, dx, dy) 
	{
		this.n_draw.on_mouse_scrolled(x, y, dx, dy)
	}

	on_key_pressed(key)
	{
		this.n_draw.on_key_pressed(key)
	}

	update_selected()
	{
		var v_selecteds = Blueprint.calc_output_value(this.n_draw, "selected")
		if (!v_selecteds) {
			return
		}

		for (var v_sel in v_selecteds.value)
		{
			var desc = v_sel.value.get_component("node_desc")
			if (desc) {
				Logger.info(desc)
			}
		}
	}
}