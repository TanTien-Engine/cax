import "blueprint.node" for Node
import "blueprint.pin" for Pin
import "blueprint.variant" for Variant, VAR_TYPE_NODE
import "blueprint.blueprint" for Blueprint
import "partgraph.variant" for VAR_TOPO_SHAPE, VAR_TOPO_FACE
import "partgraph" for BRepSelector
import "maths.vector" for Vector2
import "graphics" for Graphics

class FaceSelector is Node
{
	init()
	{
		super.init()

		this.imports = [
			Pin(this, "shape",  VAR_TOPO_SHAPE),
			Pin(this, "camera", VAR_TYPE_NODE),
		]
		this.exports = [
			Pin(this, "face", VAR_TOPO_FACE),
		]

		this.layout()

		this.v_selected = nil
	}

	calc_value(idx)
	{
		return this.v_selected
	}

	on_mouse_pressed(x, y, btn) 
	{
		var v_shape = Blueprint.calc_input_value(this, 0)
		if (!v_shape) {
			return nil
		}

		var v_cam_node = Blueprint.calc_input_value(this, 1)
		if (!v_cam_node) {
			return
		}

		var cam = v_cam_node.value.cam

		var pos = cam.position
		var dir = cam.screen2dir(Vector2(x - Graphics.get_width() * 0.5, Graphics.get_height() * 0.5 - y))
		var face = BRepSelector.select_face(v_shape.value, [ pos.x, pos.y, pos.z ], [ dir.x, dir.y, dir.z ])

		var selected = nil
		if (face) {
			selected = Variant(VAR_TOPO_FACE, face)
		} else {
			selected = nil
		}

		if (this.v_selected != selected) 
		{
			this.v_selected = selected
			Blueprint.send_pin_dirty_root(this.exports[0])
		}
	}
}